Const FIREWORK_DELAY_MIN=700
Const FIREWORK_DELAY_MAX=2000

Global FIREWORK_DELAY
Global FIREWORK_TIMESTAMP

Const HISCORE_AVAILABLE_CHARS$="@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_"

Global HUD_ENTRY_INITS_IMAGE
Global CURRENT_CHAR_FRAME

Function HiScoreEntry()
	HISCORE=SCORE
	WriteLog("Hi Score now: "+Str(HISCORE))
	
	Local HiScoreEntered=False
	HISCOREINITS=""
	HUD_ENTRY_INITS_IMAGE=0
	
	If (UI_CURRENT_MENU_IMAGE)
		FreeImage UI_CURRENT_MENU_IMAGE
		UI_CURRENT_MENU_IMAGE=0
	End If
	CURRENT_CHAR_FRAME=0
	
	Local LastFrame=0
	
	While (HiScoreEntered=False)
		
		FRAMETIMESTAMP=MilliSecs()
		
		CURRENT_CHAR_FRAME=CURRENT_CHAR_FRAME+ControlUIInputResult()
		If (LastFrame<>CURRENT_CHAR_FRAME)
			CURRENT_CHAR_FRAME=(CURRENT_CHAR_FRAME+(Len(HISCORE_AVAILABLE_CHARS))) Mod (Len(HISCORE_AVAILABLE_CHARS))
			If (UI_CURRENT_MENU_IMAGE)
				FreeImage UI_CURRENT_MENU_IMAGE
				UI_CURRENT_MENU_IMAGE=0
			End If
		End If
		
		If ((KeyHit(CTRL_KEY_LAUNCH) Or JoyHit(CTRL_JOY_LAUNCH,JOY_PORT)))
			HISCOREINITS=HISCOREINITS+(Mid(HISCORE_AVAILABLE_CHARS,CURRENT_CHAR_FRAME+1,1))
			
			If (HUD_ENTRY_INITS_IMAGE)
				FreeImage HUD_ENTRY_INITS_IMAGE
			End If	
			HUD_ENTRY_INITS_IMAGE=UI_DisplayTextImage(HISCOREINITS)
			
			If (Len(HISCOREINITS)=>MAX_INITS)
				HISCOREINITS=Left(HISCOREINITS,3)
				Exit
			End If
		End If
		
		
		If Not(UI_CURRENT_MENU_IMAGE)
			UI_CURRENT_MENU_IMAGE=UI_DisplayTextImage(Mid(HISCORE_AVAILABLE_CHARS,CURRENT_CHAR_FRAME+1,1))
		End If
		
		UpdateFireworks
		CallRender
		
	Wend
	FreeImage HUD_ENTRY_INITS_IMAGE
	HUD_ENTRY_INITS_IMAGE=0
	
	FreeImage UI_CURRENT_MENU_IMAGE
	UI_CURRENT_MENU_IMAGE=0
End Function

Function UpdateFireworks()
	If ((FRAMETIMESTAMP-FIREWORK_TIMESTAMP)>FIREWORK_DELAY)
		SetNewFireworkDelay()
		CreateFirework
	End If
	
	UpdateExplosions
	UpdateShines
End Function

Function SetNewFireworkDelay()
	FIREWORK_DELAY=Rand(FIREWORK_DELAY_MIN,FIREWORK_DELAY_MAX)
	FIREWORK_TIMESTAMP=FRAMETIMESTAMP
End Function

Function CreateFirework()
	Local X#=Rnd(0,PATTERN_BRICKS_W-1)
	Local Y#=Rnd(0,PATTERN_BRICKS_H-1)
	
	PlaceBrick(X,Y,Rand(COL_SELECTION_RED,COLOURS_SELECTION))
	While ((First BRICK_DATA)<>Null)
		ExplodeBrick(First BRICK_DATA)
		DestroyBrick First BRICK_DATA
	Wend
End Function
;~IDEal Editor Parameters:
;~F#B#46#50#55
;~C#Blitz3D